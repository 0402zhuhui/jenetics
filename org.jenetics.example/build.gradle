/*
 * Java Genetic Algorithm Library (@__identifier__@).
 * Copyright (c) @__year__@ Franz Wilhelmstötter
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Author:
 *    Franz Wilhelmstötter (franz.wilhelmstoetter@gmx.at)
 */

import org.apache.tools.ant.filters.ReplaceTokens


/**
 * @author <a href="mailto:franz.wilhelmstoetter@gmx.at">Franz Wilhelmstötter</a>
 * @since 1.2
 * @version 1.4 &mdash; <em>$Date: 2013-09-01 $</em>
 */

apply plugin: 'osgi'
apply plugin: 'eclipse'
apply plugin: 'idea'

apply from: "${rootDir}/gradle/commons.gradle"

task pack(dependsOn: build) {}

export.dependsOn(pack)
export.doLast {
	// Copy the scripts.
	copy {
		from('src/main/scripts') {
			include '**/*'
		}
		into exportScriptDir
		filter(ReplaceTokens, tokens: [
			__identifier__: identifier,
			__year__: copyrightYear,
			__version__: version
		])
	}
}

jar {
	manifest {
		version = version
		symbolicName = project.name
		name = project.name
		instruction 'Export-Package', 'org.jenetics.example'
		instruction 'Bundle-Vendor', jenetics.author
		instruction 'Bundle-Description', jenetics.description
		instruction 'Bundle-DocUR', jenetics.url

		attributes 'Implementation-Title': project.name,
					'Implementation-Version': version,
					'Implementation-URL': jenetics.url,
					'Implementation-Vendor': jenetics.name,
					'ProjectName': jenetics.name,
					'Version': version,
					'Maintainer': jenetics.author
	}
}

clean.doLast {
	file("${projectDir}/bin").deleteDir()
}

dependencies {
	compile project(':org.jenetics')
}
