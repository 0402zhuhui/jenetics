/*
 * Java Genetic Algorithm Library (@__identifier__@).
 * Copyright (c) @__year__@ Franz Wilhelmstötter
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Author:
 *    Franz Wilhelmstötter (franz.wilhelmstoetter@gmx.at)
 */

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: "java"
apply plugin: "osgi"
apply plugin: "eclipse"
apply plugin: "idea"

apply from: "../build-util.gradle"

 task pack(dependsOn: build) << {
	def exportBaseDir = new File(rootProject.buildDir, "package/${identifier}")
	def exportSourceDir = new File(exportBaseDir, "source/${project.name}")
	def exportLibDir = new File(exportBaseDir, "lib")
	def exportScriptDir = new File(exportBaseDir, "scripts")

	exportBaseDir.mkdirs()
	exportSourceDir.mkdirs()
	exportLibDir.mkdirs()
	//exportScriptDir.mkdirs()

	// Copy the library
	copy {
		from("${buildDir}/libs")
		into "${exportLibDir}"
	}

	// Copy the scripts.
	copy {
		from("./src/main") {
			include "scripts/**"
			exclude ".gradle"
		}
		into "${exportBaseDir}"
		filter(ReplaceTokens, tokens: [
			__identifier__: identifier,
			__year__: copyrightYear,
			__version__: version
		])
	}

	// Copy the sources.
	copy {
		from(".") {
			include "src/**"
			include "*.gradle"
			exclude ".gradle"
			exclude "*.iml"
			exclude "*.ipr"
			exclude "*.iws"
			exclude ".project"
			exclude ".classpath"
		}
		into "${exportSourceDir}"
		filter(ReplaceTokens, tokens: [
			__identifier__: identifier,
			__year__: copyrightYear
		])
	}
}

task export(dependsOn: pack) << {
}

jar {
	manifest {
		name = "org.jenetics.example"
		instruction "Export-Package",
				"org.jenetics.example"
		instruction "Bundle-Vendor", "Franz Wilhelmstötter"
		instruction "Bundle-Description", "Jenetics Algorithm Library"
		instruction "Bundle-DocURL", "http://jenetics.sourceforge.net"

		attributes "Implementation-Title": "org.jenetics.example",
					"Implementation-Version": version,
					"Implementation-URL": "http://jenetics.sourceforge.net",
					"Implementation-Vendor": "Jenetics",
					"ProjectName": "Jenetics",
					"Version": version,
					"Maintainer": "Franz Wilhelmstötter"
	}
}

repositories {
	flatDir(dir: "../project/lib")
}

dependencies {
	compile project(':org.jenetics')
}
