/*
 * Java Genetic Algorithm Library (@__identifier__@).
 * Copyright (c) @__year__@ Franz Wilhelmstötter
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.	See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * Author:
 *   Franz Wilhelmstötter (franz.wilhelmstoetter@gmx.at)
 *
 */

import java.text.SimpleDateFormat
import java.util.Date

import org.apache.tools.ant.filters.ReplaceTokens

import org.jenetics.gradle.task.Lyx2PDFTask

/**
 * @author <a href="mailto:franz.wilhelmstoetter@gmx.at">Franz Wilhelmstötter</a>
 * @since 1.2
 * @version 1.4 &mdash; <em>$Date$</em>
 */

apply from: "${rootDir}/commons.gradle"

// The temp build temp directory.
def buildTempDir = file("${buildDir}/tmp")

task buildManual << {
	copy {
		from("${projectDir}/src/main") {
			include 'lyx/manual.lyx'
		}
		into buildTempDir
		filter(ReplaceTokens, tokens: [
			__identifier__: identifier,
			__year__: copyrightYear,
			__identifier__: manualIdentifier
		])
	}
	copy {
		from("${projectDir}/src/main") {
			exclude 'lyx/manual.lyx'
		}
		into buildTempDir
	}
}

task lyx(type: Lyx2PDFTask) {
	document = file("${buildTempDir}/lyx/manual.lyx")
}

lyx.doLast {
	copy {
		from "${buildTempDir}/lyx/manual.pdf"
		into "${buildTempDir}/../doc"
		rename { String fileName ->
			fileName.replace('manual.pdf', "manual-${version}.pdf")
		}
	}
}

buildManual.finalizedBy(lyx)

export.dependsOn(buildManual)
export.doLast {
	// Copy the manual.
	copy {
		from("${buildDir}/doc") {
			include "*.pdf"
		}
		into exportDirs
	}
}

task clean << {
	buildDir.deleteDir()
}


